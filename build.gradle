buildscript {
	repositories {
		gradlePluginPortal()
	}
	dependencies {
		classpath 'com.modrinth.minotaur:Minotaur:2.4.3'
		classpath "com.github.breadmoirai:github-release:2.4.1"
	}
}

plugins {
	id 'maven-publish'
	id 'org.quiltmc.loom' version '0.12.+'
}

if (System.getenv("MODRINTH_TOKEN")) {
	apply plugin: "com.modrinth.minotaur"
}
if (System.getenv("GITHUB_TOKEN")) {
	apply plugin: "com.github.breadmoirai.github-release"
}

archivesBaseName = project.archives_base_name

def mc_major = '1.18'
def mc = mc_major + '.2'
def default_branch = '1.18'
def quilt_mappings = 24

def origVersion = project.version
project.version = project.version+ "+" + mc_major
group = project.maven_group

repositories {
	// Jitpack
	maven { url 'https://jitpack.io' }

	// Player Pronouns
	maven {
		name = "NucleoidMC"
		url = uri("https://maven.nucleoid.xyz/")
	}

	// Fabric Tailor
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }

	// Origins
	maven {
		name = 'Ladysnake Mods'
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
	}
	maven {
		url = 'https://maven.cafeteria.dev'
		content {
			includeGroup 'net.adriantodt.fabricmc'
		}
	}
	maven {
		url "https://maven.jamieswhiteshirt.com/libs-release"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
	maven {
		url "https://maven.shedaniel.me/"
	}
	maven {
		url "https://maven.terraformersmc.com/"
	}

	// Modrinth
	repositories {
		maven {
			name = "Modrinth"
			url = "https://api.modrinth.com/maven"
			content {
				includeGroup "maven.modrinth"
			}
		}
	}
}

dependencies {
	// // Base // //
	minecraft libs.minecraft
	mappings loom.layered {
		addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:${mc}+build.${quilt_mappings}:v2")
	}
	modImplementation libs.ql
	modImplementation libs.qsl

	// // Libraries // //
	modImplementation libs.serverTranslationsApi
	include libs.serverTranslationsApi

	// // Compat // //

	// Drogtor
	modCompileOnly libs.drogtor
	modLocalRuntime libs.drogtor

	// // Fabric Tailor
	modCompileOnly libs.fabrictailor
	modLocalRuntime libs.fabrictailor

	// Origins
	modCompileOnly libs.qfapi
	modLocalRuntime libs.qfapi
	modCompileOnly files('/build/libs/Calio-1.18.2-1.6.0.jar') // Fix quilt incompatibility
	modLocalRuntime files('/build/libs/Calio-1.18.2-1.6.0.jar') // Fix quilt incompatibility
	modCompileOnly (libs.origins) {
		exclude module: 'calio'
	}
	modLocalRuntime (libs.origins) {
		exclude module: 'calio'
	}

	// Cardinal Components
	modCompileOnly libs.cardinal
	modCompileOnly libs.cardinalEntity

	// Pehkui
	modCompileOnly libs.pehkui
	modLocalRuntime libs.pehkui

	// Fabrication
	modCompileOnly libs.fabrication
}

processResources {
	inputs.property "version", version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()

	// If this mod is going to be a library, then it should also generate Javadocs in order to aid with development.
	// Uncomment this line to generate them.
	// withJavadocJar()
}

jar {
	from("LICENSE.txt") {
		rename { "${it}_${archivesBaseName}" }
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {

	}
}

if (System.getenv("MODRINTH_TOKEN")) {
	modrinth {
		token = System.getenv("MODRINTH_TOKEN")
		projectId = 'switchy'
		versionNumber = project.version
		versionName = origVersion
		versionType = 'release'
		uploadFile = remapJar
		gameVersions = [mc]
		loaders = ['quilt']
		detectLoaders = false
		dependencies {
			required.project "qsl"
			optional.project "drogtor"
			optional.project "fabrictailor"
			optional.project "pehkui"
			optional.project "origins"
			optional.project "fabrication"
		}
		changelog = "Changelog: https://github.com/sisby-folk/switchy/releases/tag/v"+origVersion
	}
}

if (System.getenv("GITHUB_TOKEN")) {
	githubRelease {
		token System.getenv("GITHUB_TOKEN")
		owner "sisby-folk"
		repo "switchy"
		tagName 'v'+origVersion
		releaseName origVersion
		targetCommitish default_branch
		draft false
		prerelease false
		releaseAssets remapJar.archiveFile
		allowUploadToExisting true
		generateReleaseNotes true
	}
	tasks.githubRelease.dependsOn remapJar
}
