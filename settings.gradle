pluginManagement {
	repositories {
		maven {
			name = 'Quilt'
			url = 'https://maven.quiltmc.org/repository/release'
		}
		// Currently needed for Intermediary and other temporary dependencies
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		gradlePluginPortal()
	}
}

dependencyResolutionManagement {
	versionCatalogs {
		libs {
			def mc_major = '1.19'
			def mc = mc_major + '.2'

			// // Build Tools // //
			version('loom', '1.0.+')

			plugin('loom', 'org.quiltmc.loom').versionRef('loom')

			// // Base // //
			version('mc', mc)
			version('ql', "0.17.10")
			version('qm', "${mc}+build.21")
			version('qsl', "3.0.0-beta.29+${mc}")

			library('mc', 'mojang', 'minecraft').versionRef('mc')
			library('ql', 'org.quiltmc', 'quilt-loader').versionRef('ql')
			library('qm', 'org.quiltmc', 'quilt-mappings').versionRef('qm')
			library('qsl', 'org.quiltmc', 'qsl').versionRef('qsl')

			bundle('base', ['qfapi', 'ql', 'qsl'])

			// // Libraries // //
			version('serverTranslationsApi', "1.4.17+${mc}")
			version('lib39', "1.5.0-pre2+${mc}")

			library('serverTranslationsApi', 'fr.catcore', 'server-translations-api').versionRef('serverTranslationsApi')
			library('lib39Core', 'com.unascribed', 'lib39-core').versionRef('lib39')
			library('lib39Dessicant', 'com.unascribed', 'lib39-dessicant').versionRef('lib39')

			bundle('libraries', ['serverTranslationsApi', 'lib39Core', 'lib39Dessicant'])

			// // Compat // //
			version('drogtor', "1.1.3${mc_major == '1.18' ? '' : "+${mc_major}"}")
			version('fabrictailor', "1.9.0")
			version('qfapi', "4.0.0-beta.9+0.60.0-${mc}")
			version('origins', "v1.7.1")
			version('cardinal', "5.1.0")
			version('pehkui', "3.4.2")
			version('fabrication', "3.0.2+${mc_major}")
			version('placeholderApi', "2.0.0-pre.1+${mc}")
			version('playerDataApi', "0.2.2+${mc}")
			version('styledNicknames', "1.1.2+${mc_major}")
			version('styledPlayerList', "2.2.2+1.19.1")

			library('drogtor', 'maven.modrinth', 'drogtor').versionRef('drogtor')
			library('fabrictailor', 'com.github.samolego', 'FabricTailor').versionRef('fabrictailor')
			library('qfapi', 'org.quiltmc.quilted-fabric-api', 'quilted-fabric-api', ).versionRef('qfapi')
			library('origins', 'com.github.apace100', 'origins-fabric').versionRef('origins')
			library('cardinal', 'dev.onyxstudios.cardinal-components-api', 'cardinal-components-base').versionRef('cardinal')
			library('cardinalEntity', 'dev.onyxstudios.cardinal-components-api', 'cardinal-components-entity').versionRef('cardinal')
			library('pehkui', 'com.github.Virtuoel', 'Pehkui').versionRef('pehkui')
			library('fabrication', 'maven.modrinth', 'fabrication').versionRef('fabrication')
			library('placeholderApi', 'eu.pb4', 'placeholder-api').versionRef('placeholderApi')
			library('playerDataApi', 'eu.pb4', 'player-data-api').versionRef('playerDataApi')
			library('styledNicknames', 'maven.modrinth', 'styled-nicknames').versionRef('styledNicknames')
			library('styledPlayerList', 'maven.modrinth', 'styledplayerlist').versionRef('styledPlayerList')

			bundle('cardinal', [
			        'cardinal',
					'cardinalEntity',
			])
			// Note: Origins is special-cased.
			bundle('compat_compile', [
					'drogtor',
					'fabrictailor',
					'qfapi',
					'pehkui',
					'fabrication',
					'styledNicknames',
			])
			bundle('compat_runtime', [
					'fabrictailor',
					'qfapi',
					'calio',
					'pehkui',
					'playerDataApi',
					'placeholderApi',
					'styledNicknames',
					'styledPlayerList',
			])
		}
	}
}

include('core', 'cardinal', 'compat')
