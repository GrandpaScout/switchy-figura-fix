pluginManagement {
	repositories {
		maven {
			name = 'Quilt'
			url = 'https://maven.quiltmc.org/repository/release'
		}
		// Currently needed for Intermediary and other temporary dependencies
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		gradlePluginPortal()
	}
}

dependencyResolutionManagement {
	versionCatalogs {
		libs {
			def mc_major = '1.18'
			def mc = mc_major + '.2'

			// // Build Tools // //
			version('loom', '1.0.+')

			plugin('loom', 'org.quiltmc.loom').versionRef('loom')

			// // Base // //
			version('mc', mc)
			version('ql', "0.17.10")
			version('qm', "${mc}+build.26")
			version('qsl', "1.1.0-beta.26+${mc}")

			library('mc', 'mojang', 'minecraft').versionRef('mc')
			library('ql', 'org.quiltmc', 'quilt-loader').versionRef('ql')
			library('qm', 'org.quiltmc', 'quilt-mappings').versionRef('qm')
			library('qsl', 'org.quiltmc', 'qsl').versionRef('qsl')

			bundle('base', ['ql', 'qsl'])

			// // Core // //
			version('qfapi', "1.0.0-beta.28+0.67.0-${mc}")
			version('serverTranslationsApi', "1.4.12+${mc}")
			version('lib39', "1.5.0-pre3+${mc}")

			library('qfapi', 'org.quiltmc.quilted-fabric-api', 'quilted-fabric-api', ).versionRef('qfapi')
			library('serverTranslationsApi', 'fr.catcore', 'server-translations-api').versionRef('serverTranslationsApi')
			library('lib39Core', 'com.unascribed', 'lib39-core').versionRef('lib39')
			library('lib39Dessicant', 'com.unascribed', 'lib39-dessicant').versionRef('lib39')

			bundle('core', ['qfapi', 'serverTranslationsApi', 'lib39Core', 'lib39Dessicant'])

			// // Compat // //
			version('drogtor', "1.1.3${mc_major == '1.18' ? '' : "+${mc_major}"}")
			version('fabrictailor', "1.9.0-${mc}")
			version('origins', "v1.4.1")
			version('calio', "-SNAPSHOT")
			version('cardinal', "5.1.0")
			version('pehkui', "3.4.2")
			version('fabrication', "3.0.2+${mc_major}")
			version('placeholderApi', "1.1.3+1.17.1")
			version('playerDataApi', "0.1.0+1.18")
			version('styledNicknames', "1.0.4+1.18.1")
			version('styledPlayerList', "2.1.3+1.18")

			library('drogtor', 'maven.modrinth', 'drogtor').versionRef('drogtor')
			library('fabrictailor', 'com.github.samolego', 'FabricTailor').versionRef('fabrictailor')
			library('origins', 'com.github.apace100', 'origins-fabric').versionRef('origins')
			library('calio', 'com.github.LambdAurora', 'calio-test').versionRef('calio')
			library('cardinal', 'dev.onyxstudios.cardinal-components-api', 'cardinal-components-base').versionRef('cardinal')
			library('cardinalEntity', 'dev.onyxstudios.cardinal-components-api', 'cardinal-components-entity').versionRef('cardinal')
			library('pehkui', 'com.github.Virtuoel', 'Pehkui').versionRef('pehkui')
			library('fabrication', 'maven.modrinth', 'fabrication').versionRef('fabrication')
			library('placeholderApi', 'eu.pb4', 'placeholder-api').versionRef('placeholderApi')
			library('playerDataApi', 'eu.pb4', 'player-data-api').versionRef('playerDataApi')
			library('styledNicknames', 'maven.modrinth', 'styled-nicknames').versionRef('styledNicknames')
			library('styledPlayerList', 'maven.modrinth', 'styledplayerlist').versionRef('styledPlayerList')

			bundle('cardinal', [
			        'cardinal',
					'cardinalEntity',
			])
			// Note: Origins is special-cased.
			bundle('compat_compile', [
					'drogtor',
					'fabrictailor',
					'pehkui',
					'fabrication',
					'styledNicknames',
			])
			bundle('compat_runtime', [
					'drogtor',
					'fabrictailor',
					'calio',
					'pehkui'
			])
		}
	}
}

include('core', 'client', 'cardinal', 'compat')
